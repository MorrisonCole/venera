apply plugin: 'android'

android {
    compileSdkVersion 19
    buildToolsVersion '19.0.1'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "com.google.android.apps.common.testing.testrunner.GoogleInstrumentationTestRunner"
    }

    buildTypes {
        debug {
            runProguard false
        }
        release {
            runProguard false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        // Hamcrests LICENSE.txt is present in JUnit and Hamcrest, so we exclude it.
        exclude 'LICENSE.txt'
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:+'
    compile 'com.google.code.gson:gson:2.2.4'
    compile project(':splatter-sdk')

    instrumentTestCompile files('libs/espresso-contrib-1.1-bundled.jar')
}

task removeStaleResults(type: Delete) {
    FileTree tree = fileTree (dir: "${project.projectDir}/scripts/results");
    tree.each { File file ->
        delete(file)
    }
}

task pullResults(type: Exec) {
    workingDir project.projectDir

    commandLine 'scripts/pull-results.sh', "${project.buildDir}/instrumentTest-results/connected"
}

evaluationDependsOn(':splatter-instrumentation')

afterEvaluate {
    // Properties are passed to Gradle from the command line (e.g., gradle clean install -Psplatter)
    if (project.hasProperty('splatter')) {
        installDebug.dependsOn project(':splatter-instrumentation').tasks.run
        connectedInstrumentTest.dependsOn project(':splatter-instrumentation').tasks.run
        connectedInstrumentTest.dependsOn removeStaleResults
        connectedInstrumentTest.finalizedBy pullResults
    }
}
