apply plugin: 'android'

android {
    compileSdkVersion 19
    buildToolsVersion '19.0.3'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        debug {
            runProguard false
        }
        release {
            runProguard false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        // Hamcrests LICENSE.txt is present in JUnit and Hamcrest, so we exclude it.
        exclude 'LICENSE.txt'
    }
}

dependencies {
    compile 'com.google.code.gson:gson:2.2.4'
    compile project(':splatter-sdk')
}

task dexify(type: Exec, dependsOn: 'build') {
    workingDir project.projectDir

    commandLine 'scripts/dexify.sh', "${project(':dexifier').buildDir}/install/dexifier/bin/dexifier", "${project.buildDir}/apk"

    //store the output instead of printing to the console:
    standardOutput = new ByteArrayOutputStream()

    doLast {
        ext.dexifyOutputFile = new File("${project(':splatter-instrumentation').projectDir}/src/main/java/com/heisentest/splatter/dexifier", "ClassesDump.java")
        dexifyOutputFile.text = "package com.heisentest.splatter.dexifier;\n\n" + standardOutput.toString()
    }
}

evaluationDependsOn(':dexifier')

afterEvaluate {
    dexify.dependsOn project(':dexifier').tasks.installApp
}
