apply plugin: 'java'
apply plugin: 'application'

mainClassName = "com.heisentest.splatter.Main"

dependencies {
    compile files('libs/asmdex-latest.jar')
    compile files('libs/tools.jar')

    // We use a modified Soot nightly with Apache Commons CLI removed since it depends on an old version.
    compile files('libs/soot-repackaged.jar')
    compile(group: 'commons-cli', name: 'commons-cli', version: '1.3-SNAPSHOT')

    compile 'com.google.code.gson:gson:2.2.4'
    compile 'com.google.android.tools:dx:1.7'
    compile 'log4j:log4j:1.2.17'
    compile 'com.google.collections:google-collections:1.0'
    compile project(':splatter-sdk')

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

compileJava.dependsOn project(':dex-generator').tasks.dexify

task dumpDex(type: Exec) {
    workingDir project.projectDir

    commandLine 'scripts/dump.sh', "${project(':skeleton-android-app').buildDir}/apk"

    //store the output instead of printing to the console:
    standardOutput = new ByteArrayOutputStream()

    doLast {
        ext.outputFile = new File("${project.projectDir}/scripts", "dump.output")
        outputFile.text = standardOutput.toString()
    }
}

run {
    if (project.hasProperty('args')) {
        args project.args.split('\\s+')
    } else {
        args "--applicationApk=${project(':skeleton-android-app').buildDir}/apk/skeleton-android-app-debug-unaligned.apk",
             "--testApk=${project(':skeleton-android-app').buildDir}/apk/skeleton-android-app-debug-test-unaligned.apk",
             "--androidPlatforms=${project.projectDir}/android-platforms"
    }
}

run.finalizedBy dumpDex
