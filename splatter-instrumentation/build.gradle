apply plugin: 'java'
apply plugin: 'application'

mainClassName = "com.heisentest.splatter.Splatter"

dependencies {
    compile files('libs/asmdex-latest.jar')
    compile files('libs/tools.jar')
    compile files('libs/sootall-2.5.0.jar')
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'com.google.android.tools:dx:1.7'
    compile 'log4j:log4j:1.2.17'
    compile 'com.google.collections:google-collections:1.0'

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

compileJava.dependsOn project(':dexifier').tasks.dexify

task dumpDex(type: Exec) {
    workingDir project.projectDir

    commandLine 'scripts/dump.sh', "${project(':skeleton-android-app').buildDir}/apk"

    //store the output instead of printing to the console:
    standardOutput = new ByteArrayOutputStream()

    doLast {
        ext.outputFile = new File("${project.projectDir}/scripts", "dump.output")
        outputFile.text = standardOutput.toString()
    }
}

run {
    if ( project.hasProperty('args') ) {
        args project.args.split('\\s+')
    } else {
        args "${project(':skeleton-android-app').buildDir}/apk/skeleton-android-app-debug-unaligned.apk", "${project(':skeleton-android-app').buildDir}/apk/skeleton-android-app-debug-test-unaligned.apk"
    }
}

run.finalizedBy dumpDex
